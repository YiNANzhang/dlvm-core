// RUN: dlopt %s -p AD AS CSE DCE --print-ir | FileCheck %s

// CHECK: AD: changed

module "autodiff"
stage raw

// Simple scalar test (x * w + b)

func @scalar_simp: (f32, f32, f32) -> f32 {
'entry(%x: f32, %w: f32, %b: f32):
    %0.0 = multiply %x: f32, %w: f32
    %0.1 = add %0.0: f32, %b: f32
    return %0.1: f32
}

[gradient @scalar_simp]
func @scalar_simp_dxwb: (f32, f32, f32) -> (f32, f32, f32)

// CHECK-LABEL: func @scalar_simp_dxwb
// CHECK: %0.0 = literal (%w: f32, %x: f32, 1: f32): (f32, f32, f32)
// CHECK-NEXT: return %0.0: (f32, f32, f32)

[gradient @scalar_simp wrt 1, 2]
func @scalar_simp_dwb: (f32, f32, f32) -> (f32, f32)

// CHECK-LABEL: func @scalar_simp_dwb
// CHECK: %0.0 = literal (%x: f32, 1: f32): (f32, f32)
// CHECK-NEXT: return %0.0: (f32, f32)

[gradient @scalar_simp wrt 1, 2 keeping 0]
func @scalar_simp_dwb_keeping: (f32, f32, f32) -> (f32, f32, f32)

// CHECK-LABEL: func @scalar_simp_dwb_keeping
// CHECK: %0.0 = multiply %x: f32, %w: f32
// CHECK-NEXT: %0.1 = add %0.0: f32, %b: f32
// CHECK-NEXT: %0.2 = literal (%x: f32, 1: f32, %0.1: f32): (f32, f32, f32)
// CHECK-NEXT: return %0.2: (f32, f32, f32)

[gradient @scalar_simp wrt 1, 2 seedable]
func @scalar_simp_dwb_seedable: (f32, f32, f32, f32) -> (f32, f32)

// CHECK-LABEL: func @scalar_simp_dwb_seedable
// CHECK: %0.0 = literal (%x: f32, %seed: f32): (f32, f32)
// CHECK-NEXT: return %0.0: (f32, f32)

// Simple tensor test (x â€¢ w + b)

func @tensor_simp: (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>) -> <1 x 10 x f32> {
'entry(%x: <1 x 784 x f32>, %w: <784 x 10 x f32>, %b: <1 x 10 x f32>):
    %0.0 = dot %x: <1 x 784 x f32>, %w: <784 x 10 x f32>
    %0.1 = add %0.0: <1 x 10 x f32>, %b: <1 x 10 x f32>
    return %0.1: <1 x 10 x f32>
}

[gradient @tensor_simp]
func @tensor_simp_dxwb: (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>)
                       -> (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>)

// CHECK-LABEL: func @tensor_simp_dxwb
// CHECK: %0.0 = literal 1: <1 x 10 x f32>
// CHECK-NEXT: %0.1 = transpose %w: <784 x 10 x f32>
// CHECK-NEXT: %0.2 = dot %0.0: <1 x 10 x f32>, %0.1: <10 x 784 x f32>
// CHECK-NEXT: %0.3 = transpose %x: <1 x 784 x f32>
// CHECK-NEXT: %0.4 = dot %0.3: <784 x 1 x f32>, %0.0: <1 x 10 x f32>
// CHECK-NEXT: %0.5 = literal (%0.2: <1 x 784 x f32>, %0.4: <784 x 10 x f32>, %0.0: <1 x 10 x f32>): (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>)
// CHECK-NEXT: return %0.5: (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>)

[gradient @tensor_simp wrt 1, 2]
func @tensor_simp_dwb: (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>)
                      -> (<784 x 10 x f32>, <1 x 10 x f32>)

// CHECK-LABEL: func @tensor_simp_dwb
// CHECK: %0.0 = literal 1: <1 x 10 x f32>
// CHECK-NEXT: %0.1 = transpose %x: <1 x 784 x f32>
// CHECK-NEXT: %0.2 = dot %0.1: <784 x 1 x f32>, %0.0: <1 x 10 x f32>
// CHECK-NEXT: %0.3 = literal (%0.2: <784 x 10 x f32>, %0.0: <1 x 10 x f32>): (<784 x 10 x f32>, <1 x 10 x f32>)
// CHECK-NEXT: return %0.3: (<784 x 10 x f32>, <1 x 10 x f32>)


[gradient @tensor_simp wrt 1, 2 keeping 0]
func @tensor_simp_dwb_keeping: (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>)
                              -> (<784 x 10 x f32>, <1 x 10 x f32>, <1 x 10 x f32>)

// CHECK-LABEL: func @tensor_simp_dwb_keeping
// CHECK: %0.0 = literal 1: <1 x 10 x f32>
// CHECK-NEXT: %0.1 = dot %x: <1 x 784 x f32>, %w: <784 x 10 x f32>
// CHECK-NEXT: %0.2 = add %0.1: <1 x 10 x f32>, %b: <1 x 10 x f32>
// CHECK-NEXT: %0.3 = transpose %x: <1 x 784 x f32>
// CHECK-NEXT: %0.4 = dot %0.3: <784 x 1 x f32>, %0.0: <1 x 10 x f32>
// CHECK-NEXT: %0.5 = literal (%0.4: <784 x 10 x f32>, %0.0: <1 x 10 x f32>, %0.2: <1 x 10 x f32>): (<784 x 10 x f32>, <1 x 10 x f32>, <1 x 10 x f32>)
// CHECK-NEXT: return %0.5: (<784 x 10 x f32>, <1 x 10 x f32>, <1 x 10 x f32>)

[gradient @tensor_simp wrt 1, 2 seedable]
func @tensor_simp_dwb_seedable: (<1 x 784 x f32>, <784 x 10 x f32>, <1 x 10 x f32>, <1 x 10 x f32>)
                               -> (<784 x 10 x f32>, <1 x 10 x f32>)

// CHECK-LABEL: func @tensor_simp_dwb_seedable
// CHECK: %0.0 = transpose %x: <1 x 784 x f32>
// CHECK-NEXT: %0.1 = dot %0.0: <784 x 1 x f32>, %seed: <1 x 10 x f32>
// CHECK-NEXT: %0.2 = literal (%0.1: <784 x 10 x f32>, %seed: <1 x 10 x f32>): (<784 x 10 x f32>, <1 x 10 x f32>)
// CHECK-NEXT: return %0.2: (<784 x 10 x f32>, <1 x 10 x f32>)

// Differentiation source index test (`from` argument)

func @tuple_func: (f32, f32) -> (f32, f32) {
'entry(%x: f32, %y: f32):
    %0.0 = multiply %x: f32, %y: f32
    %0.1 = divide %x: f32, %y: f32
    %0.2 = literal (%0.0: f32, %0.1: f32): (f32, f32)
    return %0.2: (f32, f32)
}

[gradient @tuple_func from 0]
func @tuple_func_grad_f0: (f32, f32) -> (f32, f32)

// CHECK: func @tuple_func_grad_f0: (f32, f32) -> (f32, f32)
// CHECK: %0.0 = literal (%y: f32, %x: f32): (f32, f32)
// CHECK-NEXT: return %0.0: (f32, f32)

[gradient @tuple_func from 1]
func @tuple_func_grad_f1: (f32, f32) -> (f32, f32)

// CHECK: func @tuple_func_grad_f1: (f32, f32) -> (f32, f32)
// CHECK: %0.0 = divide 1: f32, %y: f32
// CHECK-NEXT: %0.1 = multiply %y: f32, %y: f32
// CHECK-NEXT: %0.2 = divide %x: f32, %0.1: f32
// CHECK-NEXT: %0.3 = negate %0.2: f32
// CHECK-NEXT: %0.4 = literal (%0.0: f32, %0.3: f32): (f32, f32)
// CHECK-NEXT: return %0.4: (f32, f32)

[gradient @tuple_func from 1 seedable]
func @tuple_func_grad_f1_seedable: (f32, f32, f32) -> (f32, f32)

// CHECK: func @tuple_func_grad_f1_seedable: (f32, f32, f32) -> (f32, f32)
// CHECK: %0.0 = divide %seed: f32, %y: f32
// CHECK-NEXT: %0.1 = multiply %y: f32, %y: f32
// CHECK-NEXT: %0.2 = divide %x: f32, %0.1: f32
// CHECK-NEXT: %0.3 = negate %0.2: f32
// CHECK-NEXT: %0.4 = literal (%0.0: f32, %0.3: f32): (f32, f32)
// CHECK-NEXT: return %0.4: (f32, f32)

// Function call test

func @f: (f32) -> f32 {
'entry(%x: f32):
    %0.0 = apply @mul(%x: f32, %x: f32): (f32, f32) -> f32
    return %0.0: f32
}

func @mul: (f32, f32) -> f32 {
'entry(%x: f32, %y: f32):
    %0.0 = multiply %x: f32, %y: f32
    return %0.0: f32
}

[gradient @f]
func @f_grad: (f32) -> (f32)

// CHECK: func @f_grad: (f32) -> (f32)
// CHECK: %0.0 = apply @mul_grad(%x: f32, %x: f32, 1: f32): (f32, f32, f32) -> (f32, f32)
// CHECK-NEXT: %0.1 = extract 0 from %0.0: (f32, f32)
// CHECK-NEXT: %0.2 = extract 1 from %0.0: (f32, f32)
// CHECK-NEXT: %0.3 = add %0.1: f32, %0.2: f32
// CHECK-NEXT: %0.4 = literal (%0.3: f32): (f32)
// CHECK-NEXT: return %0.4: (f32)

// CHECK: func @mul_grad: (f32, f32, f32) -> (f32, f32)
// CHECK: %0.0 = literal (%y: f32, %x: f32): (f32, f32)
// CHECK-NEXT: return %0.0: (f32, f32)
